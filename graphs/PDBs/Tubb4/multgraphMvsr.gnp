set encoding utf8
#set key inside right top offset -10.8,0 spacing 1.2
set key right offset -0,-14
set xrange[1:24]
set yrange[1:22000]
set logscale x; set format x "10^{%T}"
set logscale y; set format y "10^{%T}"
set label "Mutada (Ala302Thr) - Dinámica de 10ns" at screen 0.15,0.90 font ",18" textcolor rgb "black"

# Obtener el nombre del archivo de datos desde la línea de comandos
datname1 = ARG1

# Extraer el nombre del archivo sin la extensión para el título
#title_name = system(sprintf("basename %s .dat", datname1))

# Extraer el nombre base del archivo sin "addH-" y cambiar extensión a .pdf
output_name = system(sprintf("echo %s | sed 's/addH-//' | sed 's/.dat/.pdf/'", datname1))

#set title title_name
set xlabel 'log_{10}(r)'
set ylabel 'log_{10}(M)'

set style line 1 lc rgb "royalblue" pointtype 7
set style line 2 lc rgb "black" pointtype 7
set style line 3 lc rgb "coral" pointtype 7


set terminal postscript eps color enhanced dashlength 4 fontscale 1 linewidth 2.0 size 10cm,7cm
set tics nomirror in scale 2
# Definir la salida con el nuevo nombre
set output sprintf("| epstopdf --filter --outfile=%s", output_name)
#set output '| epstopdf --filter --outfile=1-20A.pdf'





# Función para extraer valores de los archivos
extract_params(file, line) = system(sprintf("awk 'NR==%d{print $1}' %s", line, file)) + 0

# Extraer valores de C1, m1 y R2 desde los archivos
C1_1 = extract_params("adjustdata1.dat", 1)
m1_1 = extract_params("adjustdata1.dat", 2)
R2_1 = extract_params("adjustdata1.dat", 3)

C1_2 = extract_params("adjustdata2.dat", 1)
m1_2 = extract_params("adjustdata2.dat", 2)
R2_2 = extract_params("adjustdata2.dat", 3)

C1_3 = extract_params("adjustdata3.dat", 1)
m1_3 = extract_params("adjustdata3.dat", 2)
R2_3 = extract_params("adjustdata3.dat", 3)

# Convertir C1 de logaritmo a base 10 y almacenar el valor original
C1_1_value = C1_1
C1_1 = 10**C1_1

C1_2_value = C1_2
C1_2 = 10**C1_2

C1_3_value = C1_3
C1_3 = 10**C1_3

# Graficar con los valores extraídos
plot \
    C1_1 * x**m1_1 with lines lw 1.7 lc rgb "royalblue" title sprintf('b_{1} = %.5f', m1_1), \
    C1_2 * x**m1_2 with lines lw 1.7 lc rgb "black" title sprintf('b_{2} = %.5f', m1_2), \
    C1_3 * x**m1_3 with lines lw 1.7 lc rgb "coral" title sprintf('b_{3} = %.5f', m1_3), \
    datname1 using 1:3 every ::8::18 w p ls 1 title '1-2 (\305)', \
    datname1 using 1:3 every ::19::59 w p ls 2 title '2-6 (\305)', \
    datname1 using 1:3 every ::60::99999 w p ls 3 title '6-20 (\305)'

